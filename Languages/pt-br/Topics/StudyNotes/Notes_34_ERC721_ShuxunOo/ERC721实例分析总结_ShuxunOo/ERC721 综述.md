# ERC721

O [**ERC-721**](https://eips.ethereum.org/EIPS/eip-721) fornece um mapeamento único de identificadores representando ativos individuais para endereços, criado por Dieter Shirley da Dapper Labs e popularizado pelo CryptoKitties.

Componentes principais do contrato ERC721:

- 3 contratos de biblioteca: `Address.sol`, `Context.sol` e `Strings.sol`
- 3 contratos de interface: `IERC721.sol`, `IERC721Receiver.sol`, `IERC721Metadata.sol`
- 1 contrato `EIP165`: `ERC165.sol`

Este texto foi baseado no artigo de aprendizado de Solidity de A e foi aprimorado por membros da comunidade, com exemplos específicos após cada ponto de conhecimento, com 1.9K⭐ no Github. Ainda está em constante aprimoramento, então sinta-se à vontade para contribuir. Link do repositório do artigo:

https://github.com/AmazingAng/WTFSolidity

Também foi consultado o recurso de ensino de Solidity do mestre @崔棉, link do Bilibili:

[崔棉大师的个人空间_哔哩哔哩_Bilibili](https://space.bilibili.com/286084162/?spm_id_from=333.999.0.0)

**Dependências do ERC-721**

|                     |      |                                                                                                 |                                                                                                                 |
| ------------------- | ---- | ----------------------------------------------------------------------------------------------- | --------------------------------------------------------------------------------------------------------------- |
| Nome do Contrato     | Tipo do Contrato | Link                                                                                              | Descrição                                                                                                        |
| Address.sol         | Biblioteca  | https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Address.sol  | Conjunto de funções relacionadas ao tipo de endereço, fornecendo confirmação de informações de endereço e funcionalidades de permissão.                                                                                  |
| Context.sol         | Biblioteca  | https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Context.sol  | Fornece informações sobre o contexto de execução atual, incluindo o remetente da transação e seus dados. Esses dados geralmente podem ser obtidos através de `msg`. Remetente e `msg`. Dados, não devem ser acessados de forma direta dessa maneira, pois ao lidar com metatransações, o remetente e a conta pagante da execução podem não ser o remetente real (para a aplicação). |
| Strings.sol         | Biblioteca  | https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Strings.sol  | Fornece operações básicas de strings.                                                                                                     |
| IERC721.sol         | Definição de Interface | https://eips.ethereum.org/EIPS/eip-721                                                          | Contrato principal                                                                                                             |
| IERC721Receiver.sol | Interface   | https://www.notion.so/ERC-721-4562e8d3cd3447fcbbb136f8ea4ff81b#a44b8a6172be44108c50c4304d11bd0f | O contrato de destino que recebe NFTs deve implementar esta interface                                                                                              |
| IERC721Metadata.sol | Interface   | https://www.notion.so/ERC-721-4562e8d3cd3447fcbbb136f8ea4ff81b#9558ccc545754ad5a7412c3d8138e5ad | Descrição dos metadados do NFT, onde o tokenURI é definido aqui                                                                                    |
| EIP165              | Interface   | https://eips.ethereum.org/EIPS/eip-165                                                          | Cria um método padrão para publicar e detectar interfaces implementadas por contratos inteligentes.                                                                                        |

## Diagrama de Construção do Contrato

![Untitled](./img/2-721结构.jpeg)

**Informações Adicionais**: Introdução às Tags Doxygen:

> Doxygen pode converter anotações específicas do programa em arquivos de documentação. Ele pode gerar um manual de referência puro com base na estrutura do programa, convertendo anotações de acordo com a especificação em um manual de referência puro. Ele pode visualizar as relações entre documentos, extrair a estrutura do código ou usar gráficos de dependência de inclusão, diagramas de herança e diagramas de colaboração para visualizar as relações entre documentos. Os documentos de ajuda gerados pelo Doxygen podem ser formatados como CHM, RTF, PostScript, PDF, HTML, etc.
> Mais detalhes: [Doxygen Quick Start - Zhihu (zhihu.com)](https://zhuanlan.zhihu.com/p/100223113)

**Tags Doxygen comuns na sintaxe Solidity**

| Tag     | Descrição      | Contexto de Uso do Tag |
| ------- | ------- | ----------- |
| @title  | Título do contrato   | Contrato, Interface       |
| @author | Nome do autor    | Contrato, Interface, Função    |
| @notice | Introdução funcional   | Contrato, Interface, Função    |
| @dev    | Detalhes adicionais | Contrato, Interface, Função    |
| @param  | Lista de parâmetros    | Função          |
| @return | Valor de retorno da função   | Função          |

## Contratos Padrão do ERC721

```solidity
pragma solidity ^0.8.10;

/// @title Padrão de Token Não Fungível ERC-721
/// @dev Veja https://eips.ethereum.org/EIPS/eip-721
///  Nota: o identificador ERC-165 para esta interface é 0x80ac58cd.
interface ERC721 /* é ERC165 */ {

    /// @dev Este evento é acionado quando a propriedade de um NFT é transferida (geralmente quando o usuário X chama a função transfer para transferir seu NFT para outra pessoa).
    ///   Também inclui a criação (mint `from` == 0) e destruição (destroyed`to` == 0) de tokens. Registra o endereço de envio from, o endereço de recebimento to e o tokenid.
    ///   Exceção: Durante a criação do contrato, qualquer quantidade de NFTs pode ser criada e distribuída sem acionar a Transferência.
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev Este evento é acionado quando o endereço de aprovação de um NFT é alterado ou reconfirmado. O endereço zero (0x0000000000000000000000000000000000000000) indica que não há endereço aprovado.
    ///   Quando um evento de Transferência é acionado, isso também indica que o endereço aprovado para esse NFT (se houver) foi redefinido para zero.
    ///   Registra o endereço do proprietário owner, o endereço aprovado approved e o tokenid.
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev Este evento é acionado quando um gerente de terceiros (operador) é autorizado ou desautorizado em lote.
    ///   Registra o endereço do proprietário owner, o endereço do operador aprovado e se foi aprovado approved.
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice Consulta a quantidade de NFTs sob um determinado endereço
    /// @dev Os NFTs alocados para o endereço zero são considerados inválidos, uma consulta ao endereço zero resultará em uma exceção.
    /// @param _owner Endereço para o qual a quantidade de NFTs deve ser consultada
    /// @return O valor retornado é a quantidade de NFTs que o endereço atual possui nesta série
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice Encontra o proprietário atual do NFT com o TokenID especificado
    /// @dev Os NFTs alocados para o endereço zero são considerados inválidos, uma consulta resultará em uma exceção.
    /// @param _tokenId Número de identificação do NFT
    /// @return Retorna o endereço atual que possui o NFT
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice Transfere a propriedade do NFT atual do endereço `_from` para o endereço de destino `_to`
    /// @dev Se `msg.sender` não for o proprietário atual do NFT ou um operador de terceiros autorizado, uma exceção será lançada; Se o endereço de destino `_to` for o endereço zero, uma exceção será lançada; Se o `tokenId` do NFT for inválido, uma exceção será lançada;
    ///   Durante a transferência, verifica-se se o endereço de destino `_to` é um contrato, e se for, chama a interface de recebimento ERC721 'onERC721Received'
    ///   Se o valor de retorno não for `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`, uma exceção será lançada
    ///   Isto será analisado mais detalhadamente posteriormente com o código-fonte
    /// @param _from Proprietário atual do NFT
    /// @param _to Endereço de destino para o qual o NFT atual será enviado
    /// @param _tokenId Número de identificação do NFT a ser enviado
    /// @param data Algumas informações adicionais
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice Transfere a propriedade do NFT atual do endereço `_from` para o endereço de destino `_to`
    /// @dev Funcionalidade semelhante à função `safeTransferFrom()`, mas sem dados adicionais
    /// @param _from Proprietário atual do NFT
    /// @param _to Endereço de destino para o qual o NFT atual será enviado
    /// @param _tokenId Número de identificação do NFT a ser enviado
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Transfere a propriedade do NFT atual do endereço `_from` para o endereço de destino `_to`, sem verificar se o endereço de destino implementa a interface de recebimento ERC721
    ///   O chamador do método deve garantir que o endereço de destino `_to` seja capaz de receber o NFT, caso contrário, resultará em perda permanente
    /// @dev Se `msg.sender` não for o proprietário atual do NFT ou um operador de terceiros autorizado, uma exceção será lançada; Se o endereço de destino `_to` for o endereço zero, uma exceção será lançada; Se o `tokenId` do NFT for inválido, uma exceção será lançada
    /// @param _from Proprietário atual do NFT
    /// @param _to Endereço de destino para o qual o NFT atual será enviado
    /// @param _tokenId Número de identificação do NFT a ser enviado
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Altera ou reafirma o endereço de aprovação do NFT
    /// @dev O endereço zero indica que não há endereço aprovado. Se `msg.sender` não for o proprietário atual do NFT ou um operador de terceiros autorizado, uma exceção será lançada
    /// @param _approved Novo controlador aprovado para o NFT
    /// @param _tokenId NFTID a ser aprovado
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice Autoriza ou revoga um gerente de terceiros a gerenciar todos os ativos (NFTs) sob a conta `msg.sender`
    /// @dev Aciona o evento ApprovalForAll. O contrato deve permitir que um proprietário de NFT tenha vários gerentes de terceiros autorizados
    /// @param _operator Endereço a ser adicionado à coleção de gerentes de terceiros
    /// @param _approved Se o gerente de terceiros for aprovado, é verdadeiro, se for revogado, é falso
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice Consulta a qual endereço `_owner` um NFT específico foi aprovado
    /// @dev Se o número de identificação do NFT for inválido, uma exceção será lançada
    /// @param _tokenId Número de identificação do NFT para verificar a aprovação
    /// @return Retorna o endereço de aprovação atual do NFT, se não houver, retorna o endereço zero
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice Consulta se um endereço `_owner` autorizou em lote um endereço `_operator` a gerenciar todos os ativos (NFTs)
    /// @param _owner Endereço do proprietário do NFT
    /// @param _operator Endereço do gerente de terceiros a ser verificado se foi autorizado
    /// @return Verdadeiro se `_operator` for aprovado por `_owner`, caso contrário, falso
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}
```

# Minha Experiência Pessoal:

**A necessidade é a abstração de nível mais alto**: Antes de desenvolver um projeto, é crucial entender a análise de necessidades.

**As interfaces são a segunda abstração mais alta**: Ao aprender sobre contratos inteligentes, é importante começar pelas interfaces, entendê-las completamente e, em seguida, usar esse entendimento para criticar a implementação das interfaces. É melhor não tentar inferir a funcionalidade do contrato a partir da implementação de uma interface que você não entende completamente, pois isso pode levar a uma interpretação errada.

**A definição de interfaces está acima das necessidades de primeira ordem**: Por exemplo, na minha compreensão, o contrato 721 resolve o problema da **transferência de propriedade**, e usando esse princípio, podemos continuar a melhorar a direção do contrato 721. Todos os problemas do mundo real relacionados à transferência de propriedade podem ser usados para melhorar o contrato 721 no futuro. Compras, vendas, empréstimos, resgates, hipotecas, roubos, destruição, leilões, crowdfunding, trocas, dividendos, mint em lote, transformação em lote... Todas essas aplicações do mundo real são direcionamentos para melhorar o contrato no futuro, então é importante manter-se fiel aos "primeiros princípios".

## ERC165

```solidity
interface ERC165 {
    /// @notice Verifica se o contrato implementou uma determinada interface
    /// @param interfaceID Identificador especificado em ERC-165 para identificar a interface, conforme especificado em ERC-165
    /// @dev O identificador da interface é especificado em ERC-165. Esta função usa menos de 30.000 unidades de gás
    /// @return Retorna `true` se o contrato implementar a interface `interfaceID` diferente de 0xffffffff, caso contrário, retorna `false`
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

## ERC721TokenReceiver

```solidity
// Carteiras/Corretores/Leilões que desejam aceitar transferências seguras devem implementar esta interface
/// @dev Nota: o identificador ERC-165 para esta interface é 0x150b7a02.
interface ERC721TokenReceiver {
    /// @notice Lida com a recepção de NFTs, o contrato receptor de tokens deve implementar esta interface para que a transferência seja bem-sucedida
    /// @dev Após a `transfer`, o contrato ERC721 chama esta função no receptor, se não implementar `onERC721Received`, pode lançar uma exceção e reverter a transação
    /// @param _operator Endereço que chama a função `safeTransferFrom`
    /// @param _from Endereço anterior que possuía o NFT
    /// @param _tokenId 
    /// @param _data Dados adicionais sem formato específico
    /// @return Retorna `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))` ou lança uma exceção
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

## ERC721Metadata

```solidity
/// A extensão de metadados é opcional para contratos inteligentes ERC-721 (consulte a explicação abaixo). Isso permite consultar o nome do seu contrato e os detalhes dos ativos representados pelos seus NFTs.
/// @title Padrão de Token Não Fungível ERC-721, extensão de metadados opcional
/// @dev Veja https://eips.ethereum.org/EIPS/eip-721
///  Nota: o identificador ERC-165 para esta interface é 0x5b5e139f.
interface ERC721Metadata /* é ERC721 */ {
    /// @notice Retorna um nome descritivo para a coleção de NFTs
    function name() external view returns (string memory _name);

    /// @notice O símbolo da coleção de NFTs neste contrato
    function symbol() external view returns (string  memory _symbol);

    /// @notice Retorna um identificador único de recursos uniforme (URI) para o `_tokenId` especificado.
    /// @dev Se o `_tokenId` não for um NFT válido, uma exceção será lançada. O URI é definido na RFC 3986. O URI pode apontar para um arquivo JSON que esteja em conformidade com o "ERC721"
    function tokenURI(uint256 _tokenId) external view returns (string  memory);
}
```

Para exemplificar, vamos considerar o arquivo de metadados do gato de radar #1623:

![Untitled](.\img\3.png)

```json
{
  "name": "BanaCat #1623",  // Nome do NFT
  "description": "Pixel kitty with different styles, if you're tired of those 'bored' Apes, come and take a look at these cute cats~ Maybe they are the most suitable NFT for avatar and your honey may love them too. Let's explore what the next kitty will wear on his/her head!",  // Descrição do projeto
  "image": "ipfs://QmYr9NUaom7uijzTdsjvQ57hRNV4gttnhXF7Sgvt36cUUh/1623.png",  // Localização do arquivo de imagem na rede IPFS, semelhante a um URL no protocolo HTTP
  "dna": "b39631c09c646593738fa44a1d8665cdb74faf08",  // Resumo de dados gerado a partir dos atributos do NFT, garantindo que não haja arquivos de imagem duplicados com base nesses dados
  "edition": 1623,  // Pode ser entendido como um número de identificação
  "date": 1643206138987,  // Data de criação
  "attributes": [  // Atributos da imagem
    {
      "trait_type": "background",  // Fundo laranja
      "value": "Orange"
    },
    {
      "trait_type": "head",  // Cabeça cinza
      "value": "Gray"
    },
    {
      "trait_type": "blush",  // Bochechas cor-de-rosa
      "value": "Pink"
    },
    {
      "trait_type": "nose",  // Nariz marrom
      "value": "Brown"
    },
    {
      "trait_type": "mouse",  // Boca sorridente
      "value": "Smile"
    },
    {
      "trait_type": "eyes",  // Olhos brilhantes
      "value": "Blingbling"
    },
    {
      "trait_type": "hat",  // Chapéu de radar (em homenagem à engenharia de comunicação de radar da Xidian University)
      "value": "Radar"
    }
  ],
  "Author": "shuxun"
}

Cada NFT terá um arquivo JSON correspondente, e todos os arquivos estarão em uma pasta que será enviada para a rede IPFS, gerando um endereço CID de pasta, que é o "baseURI" no contrato, e os endereços individuais dos arquivos de cada NFT são concatenados a partir desse endereço base.
```

## Address.sol (Apenas observe o método `isContract()` por enquanto)

 **Conhecimento Prévio**  

![外部账户和合约账户.png](./img/1-外部账户和合约账户.png)

Conta Externa (`EOA`): Uma conta controlada por alguém que possui a chave privada; Conta de Contrato (`CA`): Uma conta composta por um contrato inteligente, sem chave privada.

Somente contas de contrato têm código, armazenando o **codeHash**, e o método do contrato **isContract()** usa essa propriedade para determinar se o endereço de destino é um endereço de contrato.

```jsx
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (última atualização v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Coleção de funções relacionadas ao tipo de endereço
 */
library Address {
    /**
/**
     * @dev Retorna "true" se o endereço fornecido for um endereço de contrato
     * [IMPORTANTE]
     * ====
     * Quando este método retorna "false", assume-se que o endereço fornecido é um endereço de conta externa (EOA) e não um endereço de contrato, o que não é seguro
     * Entre outros, `isContract` retornará false para os seguintes
     * tipos de endereços:
     * 1. Contas externas reais (controladas por alguém que possui a chave privada);
     * 2. No construtor do contrato, passando o endereço deste contrato address(this) como parâmetro para chamar isContract();
     * 3. Um endereço onde um contrato será criado; (2, 3 adicionados por "0xMax". Entendo que não é necessário chamar isContract neste contexto de transação ou executar isContract antes de chamar Create ou create2)
     * 4. O endereço ainda existe, mas o contrato foi destruído;
     * ====
     *
     * [IMPORTANTE]
     * ====
     * Você não deve depender de `isContract` para evitar ataques de flash empréstimo!
     * Impedir chamadas de contratos é altamente desencorajado. Isso quebra a composabilidade, quebra o suporte para carteiras inteligentes
     * como Gnosis Safe e não fornece segurança, pois pode ser contornado chamando de um construtor de contrato.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // Este método depende de extcodesize/address.code.length, que retorna 0
        // para contratos em construção, já que o código é armazenado apenas no final
        // da execução do construtor.

        return account.code.length > 0;
    }

    /**
     * @dev Substitui o `transfer` do Solidity: envia `amount` wei para
     * `recipient`, encaminhando todo o gás disponível e revertendo em caso de erros.
            * Substituição para o `transfer` do Solidity: envia `amount` wei para
     * `recipient`, encaminhando todo o gás disponível e revertendo em caso de erros.
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: saldo insuficiente");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: impossível enviar valor, o destinatário pode ter revertido");
    }

    /**
     * @dev Realiza uma chamada de função Solidity usando um `call` de baixo nível. Um
     * `call` simples é uma substituição insegura para uma chamada de função: use esta
     * função em vez disso.
     *
     * Se `target` reverter com um motivo de revert, ele é repassado por esta
     * função (como chamadas de função regulares do Solidity).
     *
     * Retorna os dados brutos retornados. Para converter para o valor de retorno esperado,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requisitos:
     *
     * - `target` deve ser um contrato.
     * - chamar `target` com `data` não deve reverter.
     *
     * _Disponível desde v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: chamada de baixo nível falhou");
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-}[`functionCall`], mas com
     * `errorMessage` como motivo de revert padrão quando `target` reverte.
     *
     * _Disponível desde v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * mas também transferindo `value` wei para `target`.
     *
     * Requisitos:
     *
     * - o contrato chamador deve ter um saldo ETH de pelo menos `value`.
     * - a função Solidity chamada deve ser `payable`.
     *
     * _Disponível desde v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: chamada de baixo nível com valor falhou");
    }

    /**
     * @dev Mesmo que {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], mas
     * com `errorMessage` como motivo de revert padrão quando `target` reverte.
     *
     * _Disponível desde v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: saldo insuficiente para chamada");
        require(isContract(target), "Address: chamada para não contrato");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * mas realizando uma chamada estática.
     *
     * _Disponível desde v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: chamada estática de baixo nível falhou");
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * mas realizando uma chamada estática.
     *
     * _Disponível desde v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: chamada estática para não contrato");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * mas realizando uma chamada de delegação.
     *
     * _Disponível desde v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: chamada de delegação de baixo nível falhou");
    }

    /**
     * @dev Mesmo que {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * mas realizando uma chamada de delegação.
     *
     * _Disponível desde v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: chamada de delegação para não contrato");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Ferramenta para verificar se uma chamada de baixo nível foi bem-sucedida e reverter se não foi, seja repassando o
     * motivo de revert usando o fornecido.
     *
     * _Disponível desde v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Procura pelo motivo de revert e o repassa se presente
            if (returndata.length > 0) {
                // A maneira mais fácil de repassar o motivo de revert é usando a memória via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}
```

## Context.sol

```jsx
/**
 * @dev Fornece informações sobre o contexto de execução atual, incluindo o
 * remetente da transação e seus dados. Embora essas informações geralmente estejam disponíveis
 * via msg.sender e msg.data, elas não devem ser acessadas de maneira direta
 * pois, ao lidar com meta-transações, a conta que envia e
 * paga pela execução pode não ser o remetente real (para uma aplicação
 * é considerado).
 * Fornece informações sobre o contexto de execução atual, incluindo o
 * remetente da transação e seus dados. Embora essas informações geralmente estejam disponíveis
 * via msg.sender e msg.data, elas não devem ser acessadas de maneira direta
 * pois, ao lidar com meta-transações, a conta que envia e
 * paga pela execução pode não ser o remetente real (para uma aplicação
 * é considerado).
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}
```

## String.sol

```jsx
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (última atualização v4.7.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev Operações de string.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converte um `uint256` para sua representação decimal ASCII `string`.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspirado na implementação da OraclizeAPI - licença MIT
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converte um `uint256` para sua representação hexadecimal ASCII `string`.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converte um `uint256` para sua representação hexadecimal ASCII `string` com comprimento fixo.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: comprimento hexadecimal insuficiente");
        return string(buffer);
    }

    /**
     * @dev Converte um `address` com comprimento fixo de 20 bytes para sua representação ASCII `string` hexadecimal não verificada.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}
```